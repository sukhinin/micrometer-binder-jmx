plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.6.1'
}

group 'com.github.sukhinin'
description 'Micrometer JMX bindings'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    implementation 'io.micrometer:micrometer-core:1.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testImplementation 'org.mockito:mockito-core:3.3.0'
}

java {
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

jgitver {
    useDirty true
    useGitCommitID true
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/sukhinin/micrometer-binder-jmx'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://github.com/sukhinin/micrometer-binder-jmx/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sukhinin'
                        name = 'Boris Sukhinin'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/sukhinin/micrometer-binder-jmx.git'
                    developerConnection = 'scm:git:https://github.com/sukhinin/micrometer-binder-jmx.git'
                    url = 'https://github.com/sukhinin/micrometer-binder-jmx'
                }
            }
        }
    }
}

bintray {
    user = project.findProperty('bintray.user') ?: System.getenv('BINTRAY_USER')
    key = project.findProperty('bintray.apikey') ?: System.getenv('BINTRAY_APIKEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/sukhinin/micrometer-binder-jmx'
        issueTrackerUrl = 'https://github.com/sukhinin/micrometer-binder-jmx/issues'
        vcsUrl = 'https://github.com/sukhinin/micrometer-binder-jmx.git'
        licenses = ['Apache-2.0']
        labels = ['micrometer', 'jmx', 'kafka']
        githubRepo = 'sukhinin/micrometer-binder-jmx'
    }
}
